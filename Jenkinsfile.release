def utils, streams, s3_bucket
node {
    checkout scm
    utils = load("utils.groovy")
    streams = load("streams.groovy")
    pod = readFile(file: "manifests/pod.yaml")

    s3_bucket = utils.get_pipeline_annotation('s3-bucket')
}

properties([
    pipelineTriggers([]),
    parameters([
      choice(name: 'STREAM',
             // list devel first so that it's the default choice
             choices: (streams.development + streams.production + streams.mechanical),
             description: 'Fedora CoreOS stream to release',
             required: true),
      string(name: 'VERSION',
             description: 'Fedora CoreOS version to release',
             defaultValue: '',
             trim: true)
    ])
])

currentBuild.description = "[${params.STREAM}] Running"

// substitute the right COSA image into the pod definition before spawning it
pod = pod.replace("COREOS_ASSEMBLER_IMAGE", "coreos-assembler:master")

podTemplate(cloud: 'openshift', label: 'coreos-assembler', yaml: pod, defaultContainer: 'jnlp') {
    node('coreos-assembler') { container('coreos-assembler') {
        // Replicate the newly uploaded AMI to other regions. Intentionally
        // split out from the 'Upload AWS' stage to allow for tests to be added
        // at a later date before replicating said image.
        stage('Replicate AWS AMI') {
            s3_stream_dir = "${s3_bucket}/prod/streams/${params.STREAM}"
            utils.shwrap("""
            coreos-assembler buildprep s3://${s3_stream_dir}/builds
            coreos-assembler aws-replicate --build=${params.VERSION}
            coreos-assembler buildupload s3 --acl=public-read ${s3_stream_dir}/builds --skip-builds-json
            """)
        }

        stage('Publish') {
          // Run plume to publish official builds; This will handle modifying
          // object ACLs, modifying AMI image attributes,
          // and creating/modifying the releases.json metadata index
          utils.shwrap("""
          plume release --distro fcos \
              --version ${params.VERSION} \
              --channel ${params.STREAM} \
              --bucket ${s3_bucket}
          """)
        }
    }}
}
